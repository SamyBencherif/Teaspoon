init :

	print "First, we initialize game logic."

	require "base"
	require "graphics"
	initWindow

	speedX = 2
	speedY = 2
	posX = 225
	posY = 150

	rwall = width
	bwall = height
	rwall = sum rwall -50
	bwall = sum bwall -50

	clk = 255

	mx = 0
	my = 0
ret

physics :

	% print "Next, we make calculations related to physics."

	posX = sum posX speedX
	posY = sum posY speedY

	ifLess posX rwall skip
		ifEq posX rwall skip
			speedX = -speedX
	skip :

	ifLess posX 50
		speedX = -speedX
	end :

	ifLess posY bwall skip
		ifEq posY bwall skip
			speedY = -speedY
	skip :

	ifLess posY 50
		speedY = -speedY
	end :

ret

events :

	% print "Also, it's a good idea to handle events."

	ev = getEvent
	evCode = get ev 0
	evKey = get ev 1
	evPos = get ev 3

	% key down
	ifEq evCode 2
		% keycode is key_k
		ifEq evKey 107
			clk = 0
	end :

	% key up
	ifEq evCode 3
		% keycode is key_k
		ifEq evKey 107
			clk = 255
	end :

	% mouse move
	ifEq evCode 4

		evPosX = get evPos 0
		evPosY = get evPos 1

		mx = evPosX
		my = evPosY
	end :

	% quit event
	ifEq evCode 12
		quit
	end :
ret

render :

	% print "Rendering is the most important part."

	clear
	circle posX posY 50 255 clk clk
	line 0 0 posX posY 255 255 255 3

	px = sum mx -10
	py = sum my -10

	%    x  y  w  h  r   g   b
	rect px py 20 20 217 105 105

ret

update :

	physics $
	render $
	events $

ret